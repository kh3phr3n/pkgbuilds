# Maintainer: Remi Casanova <rc.dev@tuxico.com>
# Contributor: Mario Finelli <mario@finel.li>
# Contributor: Arthur Titeica <arthur.titeica@gmail.com>

pkgname=ttfautohint
pkgver=1.8.2
pkgrel=1

arch=('i686' 'x86_64')
depends=('pkg-config' 'freetype2' 'harfbuzz')

license=('GPL2')
url="http://www.freetype.org/ttfautohint/"
pkgdesc="Provides automated hinting process for web fonts."

sha256sums=("386741701596a8b2d5fb744901922ed2bd740490f7e6c81e5d7e83ac677889a7")
source=("https://download.savannah.gnu.org/releases/freetype/${pkgname}-${pkgver}.tar.gz")

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure --prefix=/usr --without-qt --without-doc --with-freetype-config='pkg-config freetype2' && make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver} && make -k check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver} && make DESTDIR=${pkgdir} install

    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    install -Dm644 FTL.TXT ${pkgdir}/usr/share/licenses/${pkgname}/FTL.TXT
}

