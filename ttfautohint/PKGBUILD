# Maintainer: Remi Casanova <rc.dev@tuxico.com>
# Contributor: Mario Finelli <mario@finel.li>
# Contributor: Arthur Titeica <arthur.titeica@gmail.com>

pkgname=ttfautohint
pkgver=1.6
pkgrel=1

arch=('i686' 'x86_64')
depends=('freetype2' 'harfbuzz')

license=('GPL2')
url="http://www.freetype.org/ttfautohint/"
pkgdesc="Provides automated hinting process for web fonts."

sha256sums=("e41013f710c306538ff5b2f1b4d9a5b24bda031fb73fabcaf02a21b8edd71be5")
source=("https://download.savannah.gnu.org/releases/freetype/${pkgname}-${pkgver}.tar.gz")

build() {
    cd ${srcdir}/${pkgname}-${pkgver} && ./configure --prefix=/usr --without-qt && make
}

check() {
    cd ${srcdir}/${pkgname}-${pkgver} && make -k check
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver} && make DESTDIR=${pkgdir} install

    install -Dm644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/COPYING
    install -Dm644 FTL.TXT ${pkgdir}/usr/share/licenses/${pkgname}/FTL.TXT
}

