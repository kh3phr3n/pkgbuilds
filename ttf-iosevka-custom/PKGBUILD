# Maintainer: Remi Casanova <rc.dev@tuxico.com>
# Contributor: Andy Kluger <AndyKluger@gmail.com>
# Contributor: Markus Weimar <mail@markusweimar.de>

pkgname=ttf-iosevka-custom
pkgver=1.14.0
pkgrel=1

arch=('any')
depends=('fontconfig' 'xorg-font-utils')
makedepends=('git' 'npm' 'nodejs' 'otfcc' 'ttfautohint')

license=('custom:OFL')
url="https://be5invis.github.io/Iosevka/"
pkgdesc="Spatial efficient monospace font family for programming."

source=("https://github.com/be5invis/Iosevka/archive/v${pkgver}.tar.gz")
sha256sums=("638852fa0213b7ec4df8645865cba1e08618209ce427e5052bb0fdaf35104080")

build() {
    cd ${srcdir}/Iosevka-${pkgver}
    sed -i "/^width/s/500/525/" parameters.toml
    npm install && make custom-config design='term v-brace-straight v-numbersign-slanted' && make custom
}

package() {
    install -Dm644 ${srcdir}/Iosevka-${pkgver}/dist/*/ttf/*.ttf -t ${pkgdir}/usr/share/fonts/iosevka
    install -Dm644 ${srcdir}/Iosevka-${pkgver}/LICENSE.md ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE.md
}

